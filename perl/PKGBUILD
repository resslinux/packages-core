pkgbase='perl'
pkgname=('perl-doc' 'perl-dev' 'miniperl' 'perl-utils' 'perl')
pkgver=5.27.8.r272.g1e888967e7
pkgrel=1
pkgdesc='A highly capable, feature-rich programming language'
url='https://www.perl.org/'
arch=('x86_64')
license=('Artistic GPL-2.0')
depends_doc=()
depends_dev=('perl')
depends=('musl' 'zlib' 'bzip2')
makedepends=('make' 'clang' 'bzip2-dev' 'zlib-dev')
source=('git://perl5.git.perl.org/perl.git'
        'manifest.patch'
        'usedevel.patch')
sha256sums=('SKIP'
            'c04aca0a80d598913f13e5e832eee3e8e7d621be8271c214493e842ca26a7327'
            '1e8ec6e0e1f6210b0edb32d9d978ae2443bc07d6140746c79b8c8c297e915f7e')

pkgver() {
  cd "${pkgbase}"
  
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgbase}"
  
  patch -Np1 -i "${srcdir}/manifest.patch"
  patch -Np1 -i "${srcdir}/usedevel.patch"
  
  rm -rf "cpan/Compress-Raw-Zlib/zlib-src"
  rm -rf "cpan/Compress-Raw-Bzip2/bzip2-src"
}

build() {
  cd "${pkgbase}"
  
  export BUILD_ZLIB=0
  export BUILD_BZIP2=0
  export BZIP2_LIB=/usr/lib
  export BZIP2_INCLUDE=/usr/include
  
  CFLAGS=${CFLAGS//'-flto=thin'/'-flto'}
  CXXFLAGS=${CXXFLAGS//'-flto=thin'/'-flto'}
  LDFLAGS=${LDFLAGS//'-flto=thin'/'-flto'}
  
  ./Configure -des \
              -Dusedevel \
              -Dcc=clang \
              -Dusethreads \
              -Duseshrplib \
              -Doptimize="${CFLAGS}" \
              -Dprefix='/usr' \
              -Dvendorprefix='/usr' \
              -Dprivlib='/usr/share/perl5/core_perl' \
              -Darchlib='/usr/lib/perl5/core_perl' \
              -Dsitelib='/usr/share/perl5/site_perl' \
              -Dsitearch='/usr/lib/perl5/site_perl' \
              -Dvendorlib='/usr/share/perl5/vendor_perl' \
              -Dvendorarch='/usr/lib/perl5/vendor_perl' \
              -Dinc_version_list=none \
              -Dman1ext=1perl \
              -Dman3ext=3perl \
              -Dlddlflags="-shared ${LDFLAGS}" \
              -Dldflags="${LDFLAGS}"

  make
}

package() {
  package-init
  
  cd "${pkgbase}"
  
  make DESTDIR="${tmpdir}" install
  
  find "${tmpdir}" -name '.*' -delete
}

package_miniperl() {
  install -Dm755 "${pkgbase}/miniperl" "${pkgdir}/usr/bin/miniperl"
}

package_perl-doc() {
  package-doc
  
  local file; for file in $(find "${tmpdir}" -name "*.pod"); do
    file=${file#$tmpdir/}
    install -d "${pkgdir}"/${file%/*}
    mv "${tmpdir}/${file}" "${pkgdir}/$file"
  done
}

package_perl-dev() {
  install -d "${pkgdir}/usr/lib/perl5/core_perl"
  mv "${tmpdir}/usr/lib/perl5/core_perl/Devel" "${pkgdir}/usr/lib/perl5/core_perl/"
  
  install -d "${pkgdir}/usr/bin"
  
  mv "${tmpdir}/usr/bin/h2xs" \
     "${tmpdir}/usr/bin/perlivp" \
     "${tmpdir}/usr/bin/enc2xs" \
     "${tmpdir}/usr/bin/xsubpp" \
     "${pkgdir}/usr/bin/"

  package-dev
}

package_perl-utils() {
  pkgdesc="$pkgdesc (misc utilities)"
  depends=('perl')
  
  install -d "${pkgdir}/usr/bin"
  
  local file
  for file in corelist \
              cpan \
              encguess \
              libnetcfg \
              h2ph \
              instmodsh \
              json_pp \
              perlbug \
              perlthanks \
              piconv \
              pl2pm \
              prove \
              ptar \
              ptardiff \
              ptargrep \
              shasum \
              splain \
              zipdetails
  do
    mv "${tmpdir}/usr/bin/${file}" "${pkgdir}/usr/bin/"
  done
}

package_perl() {
  package-base
}
