pkgrealname='cpython'
pkgbase='python2'
pkgname=('python2-doc' 'python2-dev' 'python2-tests' 'python2')
pkgver=2.7.14.r192.gfd9d1c901d
pkgrel=1
pkgdesc='Next generation of the python high-level scripting language'
url='https://www.python.org/'
arch=('x86_64')
license=('custom')
depends_doc=()
depends_dev=('python2')
depends=('musl')
makedepends=('libclang-dev' 'expat-dev' 'libffi-dev' 'zlib-ng-dev' 'linux-headers' 'make' 'clang')
source=('git://github.com/python/cpython.git#branch=2.7')
sha256sums=('SKIP')

pkgver() {
  cd "${pkgrealname}"
  
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgrealname}"
  
  CFLAGS=${CFLAGS//'-flto=thin'/'-flto'}
  CXXFLAGS=${CXXFLAGS//'-flto=thin'/'-flto'}
  LDFLAGS=${LDFLAGS//'-flto=thin'/'-flto'}
  
  ./configure --prefix='/usr' \
              --disable-rpath \
              --enable-ipv6 \
              --enable-loadable-sqlite-extensions \
              --enable-shared \
              --with-computed-gotos \
              --with-dbmliborder=gdbm:ndbm \
              --with-system-expat \
              --with-system-ffi \
              --with-threads

  # set thread stack size to 1MB so we don't segfault before we hit sys.getrecursionlimit()
  make EXTRA_CFLAGS="${CFLAGS} -DTHREAD_STACK_SIZE=0x100000"
}

package(){
  package-init
  
  cd "${pkgrealname}"
  
  make DESTDIR="${tmpdir}" EXTRA_CFLAGS="${CFLAGS}" install maninstall
  
  install -Dm644 LICENSE "${tmpdir}/usr/share/licenses/${pkgbase}/LICENSE"
  
  rm -r "${tmpdir}"/usr/bin/idle* "${tmpdir}"/usr/lib/python*/idlelib
  
  rm "${tmpdir}/usr/bin/2to3"
  rm "${tmpdir}/usr/bin/python"
  rm "${tmpdir}/usr/bin/python-config"
  mv "${tmpdir}/usr/bin/pydoc" "${tmpdir}/usr/bin/pydoc2"
}

package_python2-doc() {
  package-doc
}

package_python2-dev() {
  package-dev
}

package_python2-tests() {
  pkgdesc="The test modules from the main python package"
  
  local version=$(grep "m4_define(PYTHON_VERSION" "${pkgrealname}/configure.ac" | cut -d " " -f 2 | cut -d ")" -f 1)
  
  cd "${tmpdir}"/usr/lib/python${version}
  
  local i; for i in */test */tests; do
    mkdir -p "${pkgdir}"/usr/lib/python${version}/"${i}"
    mv "${i}"/* "${pkgdir}"/usr/lib/python${version}/"${i}"
    rm -rf "${i}"
  done
  
  mv "${tmpdir}"/usr/lib/python${version}/test "${pkgdir}"/usr/lib/python${version}/
}

package_python2() {
  package-base
}
